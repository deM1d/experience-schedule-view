apply plugin: 'com.android.library'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'maven-publish'
apply plugin: 'signing'

android {
    namespace 'com.idemidov.experiencescheduleview'
    compileSdk 33

    defaultConfig {
        minSdk 21
        targetSdk 33
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

group = 'com.idemidov'
version = '1.0.0'

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                artifactId = 'experiencescheduleview'

                pom {
                    name = 'Tripster experience schedule view'
                    description = 'Android view for drawing tripster experience schedule'
                    url = 'https://github.com/deM1d/experience-schedule-view'
                    licenses {
                        license {
                            name = 'Apache License 2.0'
                            url = 'https://github.com/deM1d/experience-schedule-view/blob/main/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'dem1d'
                            name = 'Ilya Demidov'
                            email = 'd3m1doff@gmail.com'
                        }

                    }
                    scm {
                        connection = 'scm:git:git://github.com/deM1d/experience-schedule-view.git'
                        developerConnection = 'scm:git:ssh://github.com/deM1d/experience-schedule-view.git'
                        url = 'https://github.com/deM1d/experience-schedule-view'
                    }
                }
            }
        }
        repositories {
            maven {
                credentials {
                    username = "$NEXUS_USERNAME"
                    password = "$NEXUS_PASSWORD"
                }

                url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            }
        }
    }

    signing {
        sign publishing.publications.release
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
}